// Top-level build file where you can add configuration options common to all sub-projects/modules.

import io.davidosemwota.microlytxtask.buildsource.BuildPlugins
import io.davidosemwota.microlytxtask.buildsource.dependencies.Libs

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.AndroidGradlePlugin.androidGradlePlugin
        classpath Libs.Kotlin.gradle_plugin

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

subprojects {
    repositories {
        google()
        jcenter()
//        mavenCentral()

//        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    apply plugin: BuildPlugins.spotless

    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(Libs.Versions.ktLint)
            licenseHeaderFile rootProject.file('.spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi'
            freeCompilerArgs += '-Xopt-in=kotlinx.coroutines.FlowPreview'
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'

            freeCompilerArgs += "-Xallow-jvm-ir-dependencies"

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}
